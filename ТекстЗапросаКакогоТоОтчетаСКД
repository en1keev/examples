ВЫБРАТЬ
	"Отбор вынесен из параметров виртуальной таблицы согласно требованию в задаче
		брать бонус последнего месяца квартала, а также чтобы если вдруг в будущем
		в бизнес-логике будет поддержана возможность ""обнулять"" бонус товара внутри квартала,
		то чтоб такой ""обнуленный"" товар не попадал в отчет" КАК КомментарийДляРазработчика,
	Т.Номенклатура КАК Номенклатура,
	Т.ТипРасчетаБюджета КАК ТипРасчетаБюджета,
	Т.ГруппаЭффективности КАК ГруппаЭффективности,
	Т.ВидБонуса КАК ВидБонуса,
	Т.ЗначениеБонуса КАК ЗначениеБонуса
ПОМЕСТИТЬ ВТ_ВсеБонусы
ИЗ
	РегистрСведений.БонусыТоваров.СрезПоследних(&ДатаОкончания, Период >= &ДатаНачала) КАК Т
ГДЕ
	ИСТИНА
	И Т.ЗначениеБонуса <> 0
	И Т.ТипРасчетаБюджета <> ЗНАЧЕНИЕ(Перечисление.ТипРасчетаБюджета.ПустаяСсылка)
	И Т.ТипРасчетаБюджета В
			(ВЫБРАТЬ
				ВЫБОР
					// MARKT-37
					КОГДА Т.ТипРасчетаБюджета = ЗНАЧЕНИЕ(Перечисление.ТипРасчетаБюджета.ВыборочныйРегламент)
						ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипРасчетаБюджета.Стандартный)
					ИНАЧЕ Т.ТипРасчетаБюджета
				КОНЕЦ КАК ТипРасчетаБюджета
			ИЗ
				РегистрСведений.ИзмененияТиповРасчетаБюджетаСети.СрезПоследних(&ДатаОкончания, Сеть = &Сеть) КАК Т)
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	"Приведение дат окончания в отборе-сравнении к одинаковой границе из-за того,
		что в контракте даты хранятся без времени дня,
		а в отчете конец стандартного периода всегда со временем,
		но тип скрытого параметра, принимающего в себя эту дату окончания,
		могут поменять по невнимательности" КАК КомментарийДляРазработчика,
	Т.Контракт КАК Контракт,
	Т.КлючПериода КАК КлючПериода,
	Т.ГруппаЭффективности КАК ГруппаЭффективности
ПОМЕСТИТЬ ВТ_ПериодыКонтрактовГдеСетьРазделяетсяПоГЭ
ИЗ
	РегистрСведений.СпискиСетейПоГруппамЭффективности КАК Т
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контракты.НастройкиОсновные КАК К
		ПО (ИСТИНА)
			И Т.Контракт = К.Ссылка
			И Т.КлючПериода = К.КлючПериода
			И (Т.Сеть = &Сеть)
			И (К.ДатаНачалаДействия = &ДатаНачала)
			И (КОНЕЦПЕРИОДА(К.ДатаОкончанияДействия, ДЕНЬ) = КОНЕЦПЕРИОДА(&ДатаОкончания, ДЕНЬ))
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	Т.Ссылка КАК Контракт,
	Т.КлючПериода КАК КлючПериода,
	Т.Номенклатура КАК Номенклатура
ПОМЕСТИТЬ ВТ_ТоварыМАГдеСетиРазделяютсяПоГЭ
ИЗ
	Справочник.Контракты.Товары КАК Т
ГДЕ
	ИСТИНА
	И Т.МаркетинговыйАссортимент
	И (Т.Ссылка, Т.КлючПериода) В
			(ВЫБРАТЬ
				ВТ.Контракт КАК Контракт,
				ВТ.КлючПериода КАК КлючПериода
			ИЗ
				ВТ_ПериодыКонтрактовГдеСетьРазделяетсяПоГЭ КАК ВТ)
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	Т.Номенклатура КАК Номенклатура,
	К.Контракт.Владелец КАК Производитель,
	К.ГруппаЭффективности КАК ГруппаЭффективности
ПОМЕСТИТЬ ВТ_ТоварыИПроизводителиГдеСетиНазначенаГЭ
ИЗ
	ВТ_ТоварыМАГдеСетиРазделяютсяПоГЭ КАК Т
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПериодыКонтрактовГдеСетьРазделяетсяПоГЭ КАК К
		ПО (ИСТИНА)
			И Т.Контракт = К.Контракт
			И Т.КлючПериода = К.КлючПериода
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	Т.Номенклатура КАК Номенклатура,
	П.Партнер КАК Производитель,
	Т.ТипРасчетаБюджета КАК ТипРасчетаБюджета,
	Т.ГруппаЭффективности КАК ГруппаЭффективности,
	Т.ВидБонуса КАК ВидБонуса,
	Т.ЗначениеБонуса КАК ЗначениеБонуса
ПОМЕСТИТЬ ВТ_БонусыТоваровСПроизводителями
ИЗ
	ВТ_ВсеБонусы КАК Т
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПоПериодамНоменклатураПроизводителей.СрезПоследних(&ДатаНачала, ) КАК П
		ПО Т.Номенклатура = П.Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	"Суть отбора: если бонус товара не разделяется по ГЭ то он выводится как есть,
		 иначе выводится бонус по той ГЭ к которой относится сеть в контракте с этим производителем.
		 Если сети нет в списке сетей производителя, то автоматически считаем ее неэффективной" КАК КомментарийДляРазработчика,
	Т.Номенклатура КАК Номенклатура,
	Ц.Цена КАК Цена_СИП,
	Т.Производитель КАК Производитель,
	Т.ТипРасчетаБюджета КАК ТипРасчетаБюджета,
	Т.ГруппаЭффективности КАК ГруппаЭффективности,
	Т.ВидБонуса КАК ВидБонуса,
	Т.ЗначениеБонуса КАК ЗначениеБонуса
{ВЫБРАТЬ
	Номенклатура.*,
	Цена_СИП,
	Производитель,
	ТипРасчетаБюджета,
	ГруппаЭффективности,
	ВидБонуса,
	ЗначениеБонуса}
ИЗ
	ВТ_БонусыТоваровСПроизводителями КАК Т
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
				&ДатаНачала,
				ИСТИНА
					И ВидЦены = ЗНАЧЕНИЕ(Справочник.ВидыЦен.CIP)
					И Период >= &ДатаНачала) КАК Ц
		ПО Т.Номенклатура = Ц.Номенклатура
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТоварыИПроизводителиГдеСетиНазначенаГЭ КАК ГЭ
		ПО (ИСТИНА)
			И Т.Номенклатура = ГЭ.Номенклатура
			И Т.Производитель = ГЭ.Производитель
ГДЕ
	(Т.ГруппаЭффективности = ЗНАЧЕНИЕ(Справочник.ГруппыЭффективностиАптечныхСетей.ПустаяСсылка)
			ИЛИ Т.ГруппаЭффективности = ЕСТЬNULL(ГЭ.ГруппаЭффективности, ЗНАЧЕНИЕ(Справочник.ГруппыЭффективностиАптечныхСетей.Неэффективные)))
{ГДЕ
	Т.ВидБонуса,
	Т.ГруппаЭффективности}
